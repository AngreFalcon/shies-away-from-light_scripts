Begin KS_JulanScript	;cribbed inexpertly from work by Grumpy, CDCooley, Melian &  TheOtherFelix. Any errors introduced are my own!

short noLore
short c_move
short compmove
short doordelay
short c_speed
short c_athletics
short j_currentmagicka
short r_check
short p_check
short mp_check
short companion
short noGreet
short oneTime
short doOnce1
short doOnce2
short talknow
short flyCheck
short wwCheck
short jul_follow
short alcohol
short weapUse
short combatCheck
short myst_level
short dest_level
short illu_level
short conj_level
short rest_level
short nospells
short pacifist
short diseased
short oldcell
short currcell
short counter2
short cdcCompanion
short armor_off
short combatmode
short mlove
short emmasnpcid
short h_check
short emb_hook_nominion
short DayCount
short NoRomance
short matchz
short tempshort
short swimcheck
short summon
short alt_level
short knockout
short underwater

float timer
float sheatheTimer
float warptimer
float wandertimer
float levtimer
float falltimer
float counter
float prevx
float currx
float swimlvl
float px
float py
float ax
float ay
float az
float bx
float by
float bz
float t1
float t2
float t3
float t4
float myz
float playz
float coDist1
float coDist2
float tempfloat

If ( OnDeath == 1 )
	Messagebox "Julan is dead."
endif

if 1
	if ( GetHealth <= 0 )
		set j_close to -10
		return
	elseif ( KS_Knockout == 1 )
		if ( GetHealthGetRatio <= 0.15 )
			if ( knockout == 0 )
				If ( GetTarget Player == 0 )
					Messagebox "Julan has been knocked unconscious."
					set knockout to 1
					StartScript KS_Jul_Knockout
				endif
			endif
		endif
		if ( knockout == 1 )
			return
		endif
	endif
endif

if ( MenuMode == 1 )
	if ( GetPCTraveling )
		set doorDelay to 1
	endif
	set j_currentmagicka to ( GetMagicka )
	if ( GetItemCount p_restore_health_b >= 1 )
		set p_check to 1
	elseif ( GetItemCount p_restore_health_c >= 1 )
		set p_check to 1
	elseif ( GetItemCount p_restore_health_e >= 1 )
		set p_check to 1
	elseif ( GetItemCount p_restore_health_q >= 1 )
		set p_check to 1
	elseif ( GetItemCount p_restore_health_s >= 1 )
		set p_check to 1
	else
		set p_check to 0
	endif
	if ( GetItemCount p_restore_magicka_b >= 1 )
		set mp_check to 1
	elseif ( GetItemCount p_restore_magicka_c >= 1 )
		set mp_check to 1
	elseif ( GetItemCount p_restore_magicka_e >= 1 )
		set mp_check to 1
	elseif ( GetItemCount p_restore_magicka_q >= 1 )
		set mp_check to 1
	elseif ( GetItemCount p_restore_magicka_s >= 1 )
		set mp_check to 1
	else
		set mp_check to 0
	endif
	if ( GetPCSleep == 0 )
		return
	endif
endif

if ( GetCurrentAIPackage != 3 )
  setspeed 40
  set j_close to 0
endif

if ( weapUse == 1 )
	if ( Player->GetWeaponType <= 8 )
		setmarksman 0
	elseif ( Player->GetWeaponType >= 9 )
		setmarksman 200
	endif
endif

;cellchanges

set tempShort to 0
set prevX to currX
set currX to ( GetPos x )
set tempFloat to ( currX - prevX )
if 1
	if ( doorDelay == -1 )	;just warped, not a cellchange
		set doorDelay to 0
	elseif ( ScriptRunning, KS_Julan_Comehere == 1 )	;summoned-warp, still move but wait  until warp is done
		set doorDelay to -2
	elseif ( doorDelay == -2 )
		set tempShort to 1
	elseif ( doorDelay == 0 )
		if ( CellChanged == 1 )
			set tempShort to 2
		elseif ( tempFloat > 256 )
			set tempShort to 1
		elseif ( tempFloat < -256 )
			set tempShort to 1
		endif
	endif
endif

if ( tempShort >= 1 )
	set oldCell to currCell
	set currCell to ( GetInterior )
	set swimCheck to 0
	set underwater to 0
	if ( GetCurrentAIPackage == 3 )
		if ( oldCell == 1 )
			set doorDelay to 1
		elseif ( currCell == 1 )
			set doorDelay to 1
		elseif ( tempShort == 1 )
			set doorDelay to 1
		endif
	endif
endif

set warpTimer to ( warpTimer - GetSecondsPassed )

if ( wanderTimer < 500 )
	set wanderTimer to ( wanderTimer - GetSecondsPassed )
	if ( warpTimer < wanderTimer )
		set warpTimer to wanderTimer
	endif
	if ( wanderTimer <= 0 )
		set wanderTimer to 600
		set KS_Jul_Nosay2 to 0
		if ( jul_follow == 1 )	;should follow
			AIFollow player 0 0 0 0
		elseif ( jul_follow == 0 )	;should wait
			AiWander 0 0 0 0 10 10 0 10 0 0 0 0
		endif
	endif
endif

;teleport door & fast travel fixes

if ( doorDelay > 0 )
	if ( doorDelay > 6 )
		if ( wanderTimer >= 500 )	;we're finished
			set doorDelay to 0
			return
		endif
	elseif ( doorDelay == 4 )	;check distance & warp to player if necessary
		if ( GetDistance Player > 800 )
			if ( ScriptRunning, KS_Jul_Doorfix == 0 )
				StartScript KS_Jul_Doorfix
			endif
		elseif ( ScriptRunning, KS_Jul_Doorfix == 0 )
			set doorDelay to 5
		endif
	elseif ( doorDelay == 5 )	;wander to get out of pile-ups
		set KS_Jul_Nosay2 to 1
		AIWander 300 0 0 0
		set wanderTimer to 1
		set doorDelay to 6
	elseif ( doorDelay != 4 )
		set doorDelay to ( doorDelay + 1 )
	endif
	set warpTimer to 4
	return
endif

;Weapon sheathing after combat checks

if 1
	if ( combatCheck == 1 )
		set warpTimer to 6
		set sheatheTimer to ( sheatheTimer - GetSecondsPassed )
		if ( GetSoundPlaying "Weapon Swish" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "crossbowShoot" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "bowShoot" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "mysticism cast" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "restoration cast" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "destruction cast" == 1 )
			set sheatheTimer to 4.4
		elseif ( GetSoundPlaying "illusion cast" == 1 )
			set sheatheTimer to 4.4
		elseif ( sheatheTimer <= 0 )   ;if successfully timed down w/ no combat sounds
			set combatCheck to 0
			set combatmode to 0
			if ( GetSpellReadied == 1 )
				cast KS_Jul_Lev_bog player
			else
				additem "silver dagger" 1
				equip "silver dagger"
	 			removeitem "silver dagger" 1
			endif
		endif
	elseif ( GetWeaponDrawn == 1 )
		set combatCheck to 1
		set combatmode to 1
		set sheatheTimer to 4.4
		set warpTimer to 6
	elseif ( GetSpellReadied == 1 )
		set combatCheck to 1
		set combatmode to 1
		set sheatheTimer to 4.4
		set warpTimer to 6
	endif
endif

;check for water in cell
if ( swimCheck == 0 )
	if ( currCell == 0 )	;exterior
		set swimCheck to 1
	elseif ( GetWindSpeed > 0.001 )	;fake exterior
		set swimCheck to 1
	elseif ( GetSoundPlaying "FootWaterLeft" == 1 )
		set swimCheck to 1
	elseif ( GetSoundPlaying "FootWaterRight" == 1 )
		set swimCheck to 1
	elseif ( GetSoundPlaying "DefaultLandWater" == 1 )
		set swimCheck to 1
	endif
endif

if ( c_move == 1 )
  if ( GetDistance Player > 85 )
    set KS_Jul_Nosay2 to 0
    AIFollow Player 0 0 0 0
    set c_move to 0
  endif
endif

if ( GetCurrentAIPackage == 3 )
	if ( jul_follow == 1 )
		if ( GetPCSleep == 1 )
			StartScript KS_Jul_Sleep
		endif

		if ( GetPCSneaking == 1 )
			ForceSneak
		elseif ( GetPCSneaking == 0 )
			ClearForceSneak
		endif

	; warping

		set px to ( Player->GetPos x )
		set py to ( Player->GetPos y )
		set playz to ( Player->GetPos z )
		set myz to ( GetPos z )

		if ( doOnce1 == 0 )
			set ax to ( Player->GetPos x )
			set ay to ( Player->GetPos y )
			set az to playz
			set doOnce1 to 1
		endif

		set t1 to ( px - ax )
		set t1 to ( t1 * t1 )
		set t2 to ( py - ay )
		set t2 to ( t2 * t2 )
		set t1 to ( t1 + t2 )
		set coDist1 to ( GetSquareRoot, t1 )

		if ( coDist1 > 360 )
			set doOnce1 to 0
		endif

		if ( coDist1 > 180 )
			if ( doOnce2 == 0 )
				set bx to ( Player->GetPos x )
				set by to ( Player->GetPos y )
				set bz to ( Player->GetPos z )
				set doOnce2 to 1
			endif
		endif

		set t3 to ( px - bx )
		set t3 to ( t3 * t3 )
		set t4 to ( py - by )
		set t4 to ( t4 * t4 )
		set t3 to ( t3 + t4 )
		set coDist2 to ( GetSquareRoot, t3 )

		if ( coDist2 > 360 )
			set doOnce2 to 0
		endif

		if ( warptimer <= 0 )
			if ( GetDistance Player > 680 )
				if ( coDist1 > 300 )
					set doordelay to -1
					SetPos x ax
					SetPos y ay
					SetPos z az
					AiFollow Player 0 0 0 0
				elseif ( coDist2 > 300 )
					set doordelay to -1
					SetPos x bx
					SetPos y by
					SetPos z bz
					AiFollow Player 0 0 0 0
				endif
			endif
		endif

		;checks for matching player z-pos
		;used while levitating, swimming, slowfalling
		if ( combatCheck > 0 )
			set matchZ to 0
		elseif ( doorDelay != 0 )
			set matchZ to 0
			elseif ( levTimer > 0 )
			set matchZ to 1
		elseif ( fallTimer > 0 )
			set matchZ to 1
		elseif ( swimCheck == 1 )
			set swimLvl to ( ( GetWaterLevel ) - 119.7 )
			if ( myZ <= swimLvl )	;check he's actually swimming
				set swimLvl to ( swimLvl - 12 )
				set tempFloat to ( swimLvl - 20 )
				if ( myZ <= swimLvl )	;if underwater
					set underwater to 1
					set matchZ to 1
				elseif ( playZ < tempFloat )	;if player underwater
					set matchZ to 1
				else
					set underwater to 0
				endif
			endif
		endif

		if ( matchZ == 1 )
			set matchZ to 0
			set tempFloat to ( myZ - playZ )
			if ( tempFloat != 0 )
				if ( tempFloat >= 5 )
					set myZ to ( myZ - 5 )
				elseif ( tempFloat <= -5 )
					set myZ to ( myZ + 5 )
				else
					set myZ to playZ
				endif
				SetPos z myZ
			endif
		endif

		if ( GetDistance Player <= 512 )
			set j_close to 1
		endif

		if ( compMove == 1 )
			if ( c_move == 0 )
				if ( GetDistance Player < 75 )
					set KS_Jul_Nosay2 to 1
					AIWander 300 0 0 0 0
					set c_move to 1
				endif
			endif
		endif
	endif
endif   ;final endif of huge block checking for AIPackage 3. i.e. follow mode

if ( GetDistance Player > 512 )
    set j_close to 0
endif

;remove levitation if no longer in follow mode and add slowfall to land safely
if ( GetCurrentAiPackage != 3 )
	set warpTimer to 6	;don't warp in wander mode!
	if ( levTimer > 0 )
		if ( fallTimer < 1 )
			set fallTimer to 1
			AddSpell KS_Jul_SlowFall
		endif
	endif
endif

;time out for levitation, not only in follow mode, so will remove levitation when waiting
if 1
	if ( levTimer > 5 )
		RemoveSpell KS_Jul_Lev
		set flyCheck to 0
		set levTimer to 0
	elseif ( levTimer > 0 )
		set levTimer to ( levTimer + GetSecondsPassed )
	endif
endif

if 1
	if ( fallTimer > 10 )
		set fallTimer to 0
		RemoveSpell KS_Jul_SlowFall
	elseif ( fallTimer > 0 )
		set fallTimer to ( fallTimer + GetSecondsPassed )
	endif
endif

set counter to ( counter + GetSecondsPassed )
if ( counter < 0.5 )
	return
endif
set counter to 0

 ;stuff that only needs to execute once every 0.5 secs

if ( GetCurrentAIPackage == 3 )
	if ( jul_follow == 1 )
		if ( GetDistance Player < 250 )
			setSpeed 60
			setAthletics 60
		elseif ( GetDistance Player > 250 )
			set c_speed to ( ( Player->GetSpeed ) * 1.25 )
			set c_athletics to ( ( Player->GetAthletics ) * 1.25 )
			setSpeed, c_speed
			setAthletics, c_athletics
		endif

		if ( Player->GetEffect sEffectLevitate == 1 )
			if ( levtimer == 0 )
				cast KS_Jul_Lev_bog player
				addspell KS_Jul_Lev
				set levtimer to 1
				set flyCheck to 1
			elseif ( levtimer > 0 )
				set levtimer to 1
			endif
		endif

		if ( player->GetEffect sEffectSlowFall == 1 )
			if ( fallTimer == 0 )
				set fallTimer to 1
				AddSpell KS_Jul_SlowFall
			elseif ( fallTimer > 0 )
				set fallTimer to 1
			endif
		endif

		if ( Player->GetEffect sEffectWaterWalking == 1 )
			if ( wwCheck == 0 )
				cast KS_Jul_WW_bog player
				addspell KS_Jul_WWalk
				set wwCheck to 1
			endif
		elseif ( wwCheck == 1 )
			removespell KS_Jul_WWalk
			set wwCheck to 0
		endif
	endif
endif  ;end of follow-mode only block

If ( KS_Julan->GetEffect sEffectPoison == 1 )
	If ( ScriptRunning "KS_Jul_Poisoned" == 0 )
		StartScript KS_Jul_Poisoned
	endif
endif

If ( GetParalysis == 1 )
	If ( GetItemCount "p_cure_paralyzation_s" > 0 )
		If ( ScriptRunning "KS_Jul_Paralyze" == 0 )
			StartScript KS_Jul_Paralyze
		endif
	endif
endif

if ( combatmode == 1 )
	return
endif

;AI fix
if ( GetCurrentAIPackage == -1 )
	if ( jul_follow == 1 )
		StartScript KS_Jul_FixAI
	endif
endif

set counter2 to ( counter2 + 1 ) ;everything after this occurs only once every 3ish secs
if ( counter2 < 5 )
	return
endif
set counter2 to 0

If ( GetHealthGetRatio < 0.5 )
		If ( j_currentmagicka >= 5 )
			If ( ScriptRunning "KS_Jul_HealSelf" == 0 )
				StartScript KS_Jul_HealSelf
			endif
		elseif ( p_check > 0 )
			If ( ScriptRunning "KS_Jul_HealSelf2" == 0 )
				StartScript KS_Jul_HealSelf2
			endif
		endif
	endif
endif

If ( mp_check > 0 )
		If ( GetMagicka < 20 )
			StartScript KS_Jul_HealMag
		endif
	endif
endif

if 1
	If ( GetCommonDisease == 1 )
		set diseased to 1
	elseif ( GetBlightDisease == 1 )
		set diseased to 2
	else
		set diseased to 0
		if ( GetEffect sEffectDamageAttribute == 1 )
			if ( GetEffect sEffectRestoreAttribute == 0 )
				if ( j_currentmagicka >= 10 )
					messagebox "Julan casts a restore attribute spell" ;pretty sure this function is bugged, have never seen him do this, even if strength drained
					cast KS_Jul_RestAttrib KS_Julan
					modcurrentmagicka -10
				endif
			endif
		endif
	endif
endif

if ( DayCount != Day )
	set talknow to ( talknow + 1 )
	set DayCount to Day
endif

if ( GetWaterBreathing != 1 )
	SetWaterBreathing 1
endif
if ( GetSwimSpeed < 55 )
	SetSwimSpeed 55
endif

if 1
	If ( GetDisposition >= 100 )
		SetDisposition 100
	elseif ( GetDisposition <= 0 )
		SetDisposition 0
	endif
endif

If ( GetJournalIndex "KS_JulanMet" >= 40 )
	ModCurrentFatigue 50
	ModCurrentMagicka 1
endif

If ( GetScale != 1.02 )
	SetScale, 1.02
endif
if 1
	If ( KS_Jul_RomState >= 4 )
		set emb_hook_nominion to 1
	else
		set emb_hook_nominion to 0
	endif
endif

set emmasnpcid to 2001
set noromance to 1

End